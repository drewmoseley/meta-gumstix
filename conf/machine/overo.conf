#@TYPE: Machine
#@NAME: Overo machine
#@DESCRIPTION: Machine configuration for the Gumstix Overo 

SOC_FAMILY = "omap3"
TARGET_ARCH = "arm"
# DEFAULTTUNE makes it possible to build for the ARM hardfloat ABI.
DEFAULTTUNE ?= "cortexa8hf-neon"

KERNEL_DEVICETREE = " \
    omap3-overo-storm-tobi.dtb omap3-overo-tobi.dtb \
    omap3-overo-storm-tobiduo.dtb omap3-overo-tobiduo.dtb \
    omap3-overo-storm-chestnut43.dtb omap3-overo-chestnut43.dtb \
    omap3-overo-storm-alto35.dtb omap3-overo-alto35.dtb \
    omap3-overo-storm-arbor43c.dtb omap3-overo-arbor43c.dtb \
    omap3-overo-storm-arbor43c1.dtb omap3-overo-arbor43c1.dtb \
    omap3-overo-storm-arbor50c.dtb omap3-overo-arbor50c.dtb \
    omap3-overo-storm-arbor70c.dtb omap3-overo-arbor70c.dtb \
    omap3-overo-storm-gallop43.dtb omap3-overo-gallop43.dtb \
    omap3-overo-storm-palo43.dtb omap3-overo-palo43.dtb \
    omap3-overo-storm-palo35.dtb omap3-overo-palo35.dtb \
    omap3-overo-storm-summit.dtb omap3-overo-summit.dtb \
    omap3-overo-storm-swd-jtag.dtb \
    omap3-overo-storm-thumbo.dtb omap3-overo-thumbo.dtb \
"

require conf/machine/include/soc-family.inc
require conf/machine/include/tune-cortexa8.inc

# Specify kernel recipe
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

# Specify u-boot parameters
PREFERRED_PROVIDER_virtual/bootloader = "u-boot"
PREFERRED_PROVIDER_u-boot = "u-boot"

UBOOT_ENTRYPOINT = "0x80008000"
UBOOT_LOADADDRESS = "0x80008000"
UBOOT_MACHINE = "omap3_overo_config"
SPL_BINARY = "MLO"
UBOOT_SUFFIX = "img"

EXTRA_IMAGEDEPENDS += "u-boot"

# Specify files for the boot partition for wic
# Note: if UBOOT_ENV is set, it should also be included here
IMAGE_BOOT_FILES ?= "u-boot.${UBOOT_SUFFIX} ${SPL_BINARY}"

# Specify xserver components for builds including X11
PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"
XSERVER ?= "xserver-xorg \
            xf86-input-evdev \
            xf86-input-keyboard \
            xf86-input-mouse \
            xf86-video-omapfb"

# Default to external video, uncomment for LCD panels
# GUI_MACHINE_CLASS = "smallscreen"
GUI_MACHINE_CLASS ?= "bigscreen"

# For kernels older than 2.6.37, use "115200 ttyS2" instead
SERIAL_CONSOLES = "115200;ttyO2"

# tar.bz2 for SD card, UBI for nand
IMAGE_FSTYPES ?= "tar.bz2 ubi"

# The magic numbers:
# 512 MiB flash = 4096 PEBs (physical erase blocks) (PEB = 128 KiB sector)
# Manufacturer's spec is minimum 4016 PEBs are good for lifetime
# Logical erase block is PEB minus 2 KiB (NAND page size) for metadata
# Partitions reserve 4+14+2+64 PEBS for xloader/u-boot/env/linux
# Minimum remaining available for rootfs: 3932 PEBs (likewise 3932 LEBs)
MKUBIFS_ARGS = "-F -m 2048 -e 126KiB -c 3932"
UBINIZE_ARGS = "-m 2048 -p 128KiB -s 512"

UBI_VOLNAME = "rootfs"

# Include wifi firmware
MACHINE_EXTRA_RDEPENDS += "linux-firmware-sd8686 linux-firmware-sd8787 linux-firmware-wl18xx"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules omap3-pwm u-boot-fw-utils"
MACHINE_FEATURES = "alsa bluetooth ext2 keyboard serial touchscreen usbgadget usbhost vfat wifi"

# TI dsplink
TOOLCHAIN_PATH ?= "${STAGING_DIR_NATIVE}${prefix_native}/bin/${TUNE_PKGARCH}${HOST_VENDOR}-${HOST_OS}"
